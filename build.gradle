buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "scala"

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.version.minecraft}-${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"//." + getBuildNumber()
group = "extracells"
archivesBaseName = "ExtraCells"
def actualVersion = version


sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}


minecraft {

    mappings channel: "stable", version: "39-1.12"

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
    /*
    replace "@VERSION@", "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
    replaceIn "Constants.java"*/
}

repositories {
    mavenCentral()
    maven {
        name = "Hwyla"
        url "https://maven.tehnut.info/"
    }
    maven {
        name = "CofhAndP455w0rd"
        url "https://maven.covers1624.net/"
    }
    maven {
        name = "Jei"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "MCMultiPart"
        url "https://maven.amadornes.com/"
    }
    maven {
        name = "BuildCraft"
        url "https://mod-buildcraft.com/maven/"
    }
    maven {
        name = "IGW"
        url "https://maven.k-4u.nl/"
    }
    maven {
        name = "Curse"
        url "https://www.cursemaven.com/"
    }
    maven {
        name = "OpenComputers"
        url "https://maven.cil.li/"
    }
    maven {
        name = "Forge"
        url = "https://maven.minecraftforge.net"
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    compileOnly fg.deobf( "cofh:RedstoneFlux:${config.version.rf}:universal")
    compileOnly fg.deobf( "p455w0rd:AE2WTLib:${config.version.ae2wtlib}")
    compileOnly fg.deobf( "p455w0rd:p455w0rdslib:${config.version.p455w0rdslib}")
    compileOnly fg.deobf( "p455w0rd:WirelessCraftingTerminal:${config.version.wct}")
    compileOnly fg.deobf( "MCMultiPart2:MCMultiPart:${config.version.mcmp}")
    compileOnly fg.deobf( "li.cil.oc:OpenComputers:${config.version.oc}")
    compileOnly fg.deobf( "igwmod:IGW-Mod-${config.version.minecraft}:${config.version.igw}:universal")
    compileOnly fg.deobf( "curse.maven:baubles-227083:${config.version.baubles}")
    compileOnly fg.deobf( "curse.maven:mekanism-268560:${config.version.mekanism}")

    implementation fg.deobf("com.mod-buildcraft:buildcraft-api:${config.version.bc}")
    runtimeOnly "com.mod-buildcraft:buildcraft-all:${config.version.bc}"

    implementation fg.deobf("mezz.jei:jei_${config.version.minecraft}:${config.version.jei}:api")
    //runtimeOnly "mezz.jei:jei_${config.version.minecraft}:${config.version.jei}"

    compileOnly ("cofh:CoFHCore:${config.version.cofhcore}:deobf") {
        transitive = false
    }
    compileOnly fg.deobf(("curse.maven:hwyla-253449:2568751")) {
        transitive = false
    }
    compileOnly fg.deobf(("curse.maven:applied-energistics-2-223794:${config.version.ae2}")){
        transitive = false
    }

    minecraft ("net.minecraftforge:forge:1.12.2-14.23.5.2860") {
        exclude group: 'org.scala-lang'
    }
    implementation 'org.scala-lang:scala-library:2.12.18'
}

processResources {
    duplicatesStrategy=DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'
        include '**/*.properties'
        expand 'version': project.version, 'mcversion': "1.12.2"
    }
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'
        include '**/*.properties'
        expand 'version': "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}", 'mcversion': "1.12.2"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.scala
    from sourceSets.main.java
    archiveClassifier = 'api'
	include 'extracells/api/**'
}

artifacts {
    archives apiJar
}
